    inl layer_norm =
        inl mean_fwd i s =
            inl n = (primal i).dim |> snd |> HostTensor.span |> to float
            s.CudaKernel.map_d1_seq_broadcast {
                seq = 
                    {
                    redo=(+)
                    map_out=inl i sum -> i - sum / n
                    }
                } (primal i)

        inl mean_bck r i s =
            inl n = (primal i).dim |> snd |> HostTensor.span |> to float
            s.CudaKernel.map_d1_seq_broadcast' {
                seq = 
                    {
                    redo=(+)
                    map_out=inl dv dv_mean adjoint -> adjoint + dv - dv_mean / n
                    }
                } (adjoint r) (adjoint i)
            s.CudaTensor.print (adjoint i)

        inl mean i s =
            inl r = mean_fwd i s |> dr s
            r, inl _ -> mean_bck r i s 

        inl norm_fwd i s = 
            inl n = (primal i).dim |> snd |> HostTensor.span |> to float
            inl r = 
                s.CudaKernel.map_d1_seq_broadcast {
                    seq = 
                        {
                        map_in=inl v -> v*v
                        redo=(+)
                        map_out=inl v vv -> v / sqrt (vv / n)
                        }
                    } (primal i)
            //s.CudaTensor.print r
            r 

        inl norm_bck r i s =
            inl n = (primal i).dim |> snd |> HostTensor.span |> to float
            s.CudaKernel.map_d1_seq_broadcast' {
                seq = 
                    {
                    map_in=inl dr,v -> v*v
                    redo=(+)
                    map_out=inl dr,v vv -> dr,v,sqrt (vv / n)
                    }
                    ,
                    {
                    map_in=inl dr,v,norm -> -dr * v / (norm * norm)
                    redo=(+)
                    map_out=inl dr,v,norm bot adjoint -> 
                        inl top = dr / norm
                        inl bot = (bot * v) / (norm * n)
                        adjoint + top + bot
                    }
                } (adjoint r, primal i) (adjoint i)
            //s.CudaTensor.print (adjoint i)

        inl norm i s =
            inl r = norm_fwd i s |> dr s
            r, inl _ -> norm_bck r i s 

        inl activation i =
            inm v = mean i
            norm v

        activation